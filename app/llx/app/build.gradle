plugins {
    alias(libs.plugins.android)
    alias(libs.plugins.kotlin)
}

def appendVersionNameVersionCode(variant, defaultConfig) {
    variant.outputs.all { output ->
        if (output.zipAlign) {
            def file = output.outputFile
            def fileName = file.name.replace(".apk", "-" + defaultConfig.versionCode + ".apk")
            fileName = fileName.replace("app", "lightning_launcher")
            output.outputFileName = fileName
        }

//        def file = output.packageApplication.file
//        def fileName = file.name.replace(".apk", "-" + defaultConfig.versionCode + ".apk")
//        fileName = fileName.replace("app", "lightning_launcher")
//        output.packageApplication.outputFileName = fileName
    }
}

if(project.hasProperty("signing.properties") && new File(project.property("signing.properties")).exists()) {

    Properties props = new Properties()
    props.load(new FileInputStream(file(project.property("signing.properties"))))

    android {
        signingConfigs {
            release {
                storeFile file(props['sp.storeFile'])
                keyAlias props['sp.keyAlias']
                storePassword props['sp.storePassword']
                keyPassword props['sp.keyPassword']
            }
        }

        buildTypes {
            debug {
                signingConfig signingConfigs.release
            }
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

android {
    compileSdk 33

    defaultConfig {
        versionCode 330
        versionName "14.3 (6de3540)"
        buildConfigField "boolean", "IS_BETA", "false" //  change in core too
        buildConfigField "boolean", "HAS_UEC", "false"

        minSdk 26
        targetSdk 33
    }

//    dexOptions {
//        incremental true
//    }

    buildTypes {
        debug {
            manifestPlaceholders += [provider_authority: "net.pierrox.lightning_launcher"]
        }
        release {
            manifestPlaceholders += [provider_authority: "net.pierrox.lightning_launcher_extreme"]

            minifyEnabled true
            proguardFiles 'proguard.cfg'

            applicationVariants.all { variant ->
                appendVersionNameVersionCode(variant, defaultConfig)
            }
        }
    }

    flavorDimensions "version"

    productFlavors {
        extreme {
            manifestPlaceholders += [app_name: "LLAppExtreme"]
            applicationId "net.pierrox.lightning_launcher_extreme"
            dimension "version"
        }
    }
    buildFeatures {
        aidl true
    }
    namespace 'net.pierrox.lightning_launcher_extreme'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation project(':core')
    implementation project(':plugin-api')
    implementation libs.lsvg
    implementation libs.flows
    implementation libs.koin.android
    implementation libs.androidx.appcomat
    implementation libs.androidx.material
    implementation libs.androidx.preference
}
